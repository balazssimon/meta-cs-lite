namespace MetaDslx.Bootstrap.MetaCompiler.Compiler;

using System;
using MetaDslx.CodeAnalysis.Annotations;
using MetaDslx.CodeAnalysis.Binding;
using MetaDslx.CodeAnalysis.Syntax;
using MetaDslx.CodeAnalysis;
using MetaDslx.CodeAnalysis.Symbols;
using MetaDslx.Modeling;
using MetaDslx.Bootstrap.MetaCompiler.Model;
using MetaDslx.Bootstrap.MetaCompiler.Symbols;
//using metamodel MetaDslx.Bootstrap.MetaCompiler.Model.Compiler;

language Compiler;

Main returns Namespace:
	'namespace' name=[Name(qualifierProperty:Declarations)]Qualifier ';'
	Using*
	Declarations
	eof;

Using returns ImportSymbol
	: 'using' namespaces+=#NamespaceSymbol ';'
	| alt UsingMetaModel returns ImportMetaModelSymbol: 'using' 'metamodel' symbols+=#DeclaredSymbol ';'
	| alt UsingSymbols returns ImportSymbolModelSymbol: 'using' 'symbols' namespaces+=#NamespaceSymbol ';';

[Scope]
block Declarations: declarations+=LanguageDeclaration declarations+=Rule*;

LanguageDeclaration returns MetaDslx.Bootstrap.MetaCompiler.Model.Language: 'language' name=Name ';' grammar=Grammar;

Grammar: [Scope](rules+=Rule*);

Rule: ParserRule | LexerRule;

ParserRule: annotations+=ParserAnnotation* (isBlock?='block' name=Name | returnType+=[Use(MetaType)]Identifier | name=Name 'returns' returnType+=#MetaType) ':' alternatives+=PAlternative ('|' alternatives+=PAlternative)* ';';
PAlternative: (annotations+=ParserAnnotation* 'alt' name=Name? ('returns' returnType+=#MetaType)? ':')? elements+=PElement* ('=>' returnValue=Expression)?;
PElement: (nameAnnotations+=ParserAnnotation* symbolProperty+=[SymbolProperty]Identifier assignment=Assignment)? valueAnnotations+=ParserAnnotation* value=PElementValue multiplicity=Multiplicity?;
PElementValue: PBlock | PEof | PKeyword | PReference;
PReference: rule=[Use(Rule)]Identifier | '#' referencedTypes+=#MetaType | '#{' referencedTypes+=#MetaType (',' referencedTypes+=#MetaType)* '}' ;
PEof: 'eof';
PKeyword: text=TString;
PBlock: '(' alternatives+=PBlockAlternative ('|' alternatives+=PBlockAlternative)* ')';
PBlockAlternative returns PAlternative: elements+=PElement* ('=>' returnValue=Expression)?;

LexerRule: annotations+=LexerAnnotation* ('token' name=Name ('returns' returnType=#MetaType)? | isHidden?='hidden' name=Name | isFragment?='fragment' name=Name) ':' alternatives+=LAlternative ('|' alternatives+=LAlternative)* ';';
LAlternative: elements+=LElement*;
LElement: isNegated?='~'? value=LElementValue multiplicity=Multiplicity?;
LElementValue: LBlock | LFixed | LWildCard | LRange | LReference /*| LSet*/;
LReference: rule=[Use(LexerRule)]Identifier;
LFixed: text=TString;
LWildCard: '.';
LRange: startChar=TString '..' endChar=TString;
// TODO: add sets if lexer modes are available
//LSet: '[' items+=LSetItem* ']';
//LSetItem: LSetChar | LSetRange;
//LSetChar: 
//LSetRange:
LBlock: '(' alternatives+=LAlternative ('|' alternatives+=LAlternative)* ')';

Expression: NullExpression | BoolExpression | IntExpression | StringExpression | ReferenceExpression | ArrayExpression;
NullExpression: 'null';
BoolExpression: boolValue?='true' | 'false';
IntExpression: intValue=TInteger;
StringExpression: stringValue=TString;
ReferenceExpression: symbolValue+=[ConvertToExpectedType]Qualifier;
ArrayExpression: '{' (items+=Expression (',' items+=Expression)*)? '}';

ParserAnnotation returns MetaDslx.Bootstrap.MetaCompiler.Model.Annotation: '[' type=[Use(types:{Binder,MetaDslx.CodeAnalysis.Annotations.Annotation},suffixes:{"Binder","Annotation"})]Qualifier AnnotationArguments? ']';
LexerAnnotation returns MetaDslx.Bootstrap.MetaCompiler.Model.Annotation: '[' type=[Use(types:{TokenKind,MetaDslx.CodeAnalysis.Annotations.Annotation},suffixes:{"TokenKind","Annotation"})]Qualifier AnnotationArguments? ']';
block AnnotationArguments: '(' arguments+=AnnotationArgument (',' arguments+=AnnotationArgument)* ')';
AnnotationArgument: (namedParameter+=[AnnotationArgument]Identifier ':')? value=Expression;

Assignment
	: '=' => Assign
	| '?=' => QuestionAssign
	| '!=' => NegatedAssign
	| '+=' => PlusAssign
	;

Multiplicity
	: '?' => ZeroOrOne
	| '*' => ZeroOrMore
	| '+' => OneOrMore
	| '??' => NonGreedyZeroOrOne
	| '*?' => NonGreedyZeroOrMore
	| '+?' => NonGreedyOneOrMore
	;


[Name]
block Name: Identifier;

[DefaultReference]
[Qualifier]
block Qualifier: Identifier ('.' Identifier)*;

block QualifierList: Qualifier (',' Qualifier)*;

[Identifier]
block Identifier: TPrimitiveType | TIdentifier | TVerbatimIdentifier;

[Number]
token TInteger returns int32: '0'| '1'..'9' ('0'..'9')* ;

[Number]
token TDecimal returns double: ('0'|'1'..'9' ('0'..'9')*) '.' ('0'..'9')+ ;

[Keyword]
token TPrimitiveType returns MetaType
	: 'bool'
	| 'int'
	| 'string'
	| 'type'
	| 'symbol'
	| 'object'
	| 'void'
	;

[DefaultIdentifier]
token TIdentifier: ('_'|'a'..'z'|'A'..'Z')+('_'|'a'..'z'|'A'..'Z'|'0'..'9')*;
[Identifier]
token TVerbatimIdentifier: '@'('_'|'a'..'z'|'A'..'Z')+('_'|'a'..'z'|'A'..'Z'|'0'..'9')*;

[DefaultSeparator]
token TComma: ',' ;

[String]
token TString returns string
    : '"' DoubleQuoteTextCharacter* '"'
    | '\'' SingleQuoteTextCharacter* '\'';

fragment DoubleQuoteTextCharacter : DoubleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment DoubleQuoteTextSimple : ~('"' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment SingleQuoteTextCharacter : SingleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment SingleQuoteTextSimple : ~('\'' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment CharacterEscapeSimple : '\\' CharacterEscapeSimpleCharacter;
fragment CharacterEscapeSimpleCharacter : '\'' | '"' | '\\' | '0' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v'; 
fragment CharacterEscapeUnicode
    : '\\u' HexDigit HexDigit HexDigit HexDigit
    | '\\U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit;
fragment HexDigit : ('0'..'9' | 'a'..'f' | 'A'..'F');

[Whitespace]
token Utf8Bom: '\u00EF' '\u00BB' '\u00BF';

[DefaultWhitespace]
token TWhitespace: ('\t'|' ') +;

[DefaultEndOfLine]
token TLineEnd: ('\r\n' | '\r' | '\n');

[SingleLineComment]
token TSingleLineComment: '//' ~('\r' | '\n')*;

[MultiLineComment]
token TMultiLineComment: '/*' .*? '*/';

token TInvalidToken: .;
