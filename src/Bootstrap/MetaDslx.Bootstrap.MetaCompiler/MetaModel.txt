namespace MetaDslx.Examples.MetaModel;

using System;
using MetaDslx.CodeAnalysis.Annotations;
using MetaDslx.CodeAnalysis.Binding;
using MetaDslx.Examples.MetaModel.Model;
using MetaDslx.Examples.MetaModel;

language MetaModel;

[Root(MetaNamespace)]
main : namespaceDeclaration eof;

[Define(MetaNamespace)]
namespaceDeclaration : 'namespace' [Nesting(MetaNamespace,Declarations)] qualifiedName ';' usingDeclarations declarations;

usingDeclarations : usingDeclaration*;

[Import]
usingDeclaration : 'using' [Use(MetaNamespace)] qualifier ';';

[Scope]
[Property(Declarations)]
declarations : metaModelDeclaration declaration*;

[Define(MetaModel)]
metaModelDeclaration : 'metamodel' name ';';

declaration : enumDeclaration | classDeclaration | associationDeclaration;

[Define(MetaEnum)]
enumDeclaration : 'enum' name enumBody;
[Scope]
enumBody : '{' enumLiterals? '}';
[Property(EnumLiterals)]
enumLiterals : enumLiteral (',' enumLiteral)* ;

[Define(MetaEnumLiteral)]
enumLiteral : name;

[Define(MetaClass)]
classDeclaration : [Property(IsAbstract,true)] 'abstract'? 'class' name (':' [Property(BaseClasses)][Use(MetaClass)] qualifierList)? classBody;
[Scope]
classBody : '{' [Property(Properties)] propertyDeclaration* '}';

[Define(MetaProperty)]
propertyDeclaration : [Property(Kind)] propertyModifier? [Property(Type)] typeReference name propertyRedefinition? propertySubsetting? ';';

propertyModifier : [Constant(MetaPropertyKind.Derived)] 'derived' | [Property(IsContainment,true)] 'containment' | [Constant(MetaPropertyKind.Derived)] 'readonly';
propertyRedefinition : 'redefines' [Property(RedefinedProperties)][Use(MetaProperty)] qualifierList;
propertySubsetting : 'subsets' [Property(SubsettedProperties)][Use(MetaProperty)] qualifierList;

[Association]
associationDeclaration : 'association' [Use(MetaProperty)] qualifier 'with' [Use(MetaProperty)] qualifier ';';

typeReference : primitiveType | collectionType | nullableType | [Use(MetaType)] qualifier;

primitiveType 
	: [Constant(Meta.Int)] 'int' 
	| [Constant(Meta.String)] 'string' 
	| [Constant(Meta.Bool)] 'bool';

[Define(MetaCollectionType)]
collectionType : ([Property(Kind,MetaCollectionKind.List)] 'list' | [Property(Kind,MetaCollectionKind.Set)] 'set') '<' [Property(InnerType)] typeReference '>';
[Define(MetaNullableType)]
nullableType : [Property(InnerType)] typeReference '?';

[Name]
qualifiedName : qualifier;

qualifierList : qualifier (',' qualifier)*;

[Qualifier]
qualifier : identifier ('.' identifier)*;

[Name]
name : identifier;

[Identifier]
identifier : TIdentifier;

[TokenKind(Number)]
TInteger : '0'| '1'..'9' ('0'..'9')* ;

[TokenKind(Number)]
TDecimal : ('0'|'1'..'9' ('0'..'9')*) '.' ('0'..'9')+ ;

[TokenKind(Identifier,true)]
TIdentifier : ('_'|'a'..'z'|'A'..'Z')+('_'|'a'..'z'|'A'..'Z'|'0'..'9')*;

[TokenKind(Separator,true)]
TComma : ',' ;

[TokenKind(String)]
TString : '"' .*? '"';

[TokenKind(Whitespace,true)]
hidden TWhitespace : ('\t'|' ') +;

[TokenKind(EndOfLine,true)]
hidden TLineEnd : ('\r\n' | '\r' | '\n');

[TokenKind(Comment)]
hidden TSingleLineComment : '//' ~('\r' | '\n')*;

[TokenKind(Comment)]
hidden TMultiLineComment : '/*' .*? '*/';
