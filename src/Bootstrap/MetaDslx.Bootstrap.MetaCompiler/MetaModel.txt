namespace MetaDslx.Examples.MetaModel;

using System;
using MetaDslx.CodeAnalysis.Annotations;
using MetaDslx.CodeAnalysis.Binding;
using MetaDslx.Examples.MetaModel.Model;

language MetaModel;

[Root(MetaNamespace)]
main : namespaceDeclaration usingDeclaration* declaration* eof;

[Define(MetaNamespace)]
namespaceDeclaration : 'namespace' [Nesting(MetaNamespace,Declarations)] qualifiedName ';';

usingDeclaration : 'using' qualifier ';';

declaration : enumDeclaration | classDeclaration | associationDeclaration;

[Define(MetaEnum)]
enumDeclaration : 'enum' name '{' enumLiterals? '}';

enumLiterals : enumLiteral (',' enumLiteral)* ;

[Define(MetaEnumLiteral)]
enumLiteral : name;

[Define(MetaClass)]
classDeclaration : isAbstract?='abstract'? 'class' name (':' qualifier (',' qualifier)*)? '{' propertyDeclaration* '}';

[Define(MetaProperty)]
propertyDeclaration : propertyModifier? typeReference name propertyRedefinition? propertySubsetting? ';';

propertyModifier : 'derived' | 'containment' | 'readonly';
propertyRedefinition : 'redefines' qualifiedName (',' qualifiedName);
propertySubsetting : 'subsets' qualifiedName (',' qualifiedName);

associationDeclaration : 'association' qualifier 'with' qualifier ';';

typeReference : primitiveType | collectionType | qualifier;

primitiveType 
	: [Constant(Meta.Int)] 'int' 
	| [Constant(Meta.String)] 'string' 
	| [Constant(Meta.Bool)] 'bool';

collectionType : ('list' | 'set') '<' typeReference '>';

[Name]
qualifiedName : qualifier;

[Qualifier]
qualifier : identifier ('.' identifier)*;

[Name]
name : identifier;

[Identifier]
identifier : TIdentifier;

[TokenKind(Number)]
TInteger : '0'| '1'..'9' ('0'..'9')* ;

[TokenKind(Number)]
TDecimal : ('0'|'1'..'9' ('0'..'9')*) '.' ('0'..'9')+ ;

[TokenKind(Identifier,true)]
TIdentifier : ('_'|'a'..'z'|'A'..'Z')+('_'|'a'..'z'|'A'..'Z'|'0'..'9')*;

[TokenKind(Separator,true)]
TComma : ',' ;

[TokenKind(String)]
TString : '"' .*? '"';

[TokenKind(Whitespace,true)]
hidden TWhitespace : ('\t'|' ') +;

[TokenKind(EndOfLine,true)]
hidden TLineEnd : ('\r\n' | '\r' | '\n');

[TokenKind(Comment)]
hidden TSingleLineComment : '//' ~('\r' | '\n')*;

[TokenKind(Comment)]
hidden TMultiLineComment : '/*' .*? '*/';
